@Metadata.layer: #CORE
@UI.headerInfo.typeNamePlural: 'Stocks Inventory'
@UI.headerInfo.typeName: 'Stock Inventory'
@UI.headerInfo.title: { type: #STANDARD, value: 'MaterialDescription' }
@UI.headerInfo.description: { type: #STANDARD, value: 'MaterialId' }
annotate view ZCDR_INV_STOCK with
{
  @UI.facet: [

   {
    purpose: #HEADER,
    position: 10 ,
    type: #DATAPOINT_REFERENCE,
    targetQualifier: 'CurrentStockData'
  },
  {
    purpose: #HEADER,
    position: 20 ,
    type: #DATAPOINT_REFERENCE,
    targetQualifier: 'StatusData'
  },
  {
    label: 'General Information',
    type: #COLLECTION,  /* empty container on the object page body to hold subsections */
    id: 'GeneralInfo',
    position: 10
   },
   {
     label: 'General',
     type: #IDENTIFICATION_REFERENCE,  /* identification subsection */
     parentId: 'GeneralInfo'  /*The section id*/
   },
   {
    label: 'Stock Info',
    purpose: #STANDARD,
    position: 20,
    type: #FIELDGROUP_REFERENCE,
    parentId: 'GeneralInfo',
    targetQualifier: 'StocksGroup'
   },
   {
    label: 'Purchase Req. Info',
    purpose: #STANDARD,
    position: 30,
    type: #FIELDGROUP_REFERENCE,
    parentId: 'GeneralInfo',
    targetQualifier: 'PurchGroup'
   } ]




  @EndUserText.label: 'Material Id'
  @UI.identification: [{ position: 10 , label: 'Material Id' }]
  @UI.lineItem: [{ position: 10 , label: 'Material Id' }]
  @UI.selectionField: [ {position: 10 }]
  MaterialId;

  @EndUserText.label: 'Material Description'
  @UI.identification: [{ position: 20 , label: 'Material Description' }]
  @UI.lineItem: [{ position: 20, label: 'Material Description' }]
  @UI.selectionField: [{ position: 20 }]
  MaterialDescription;

  @EndUserText.label: 'Plant Id'
  @UI.identification: [{ position: 29, label: 'Plant Id' }]
  @UI.lineItem: [{ position: 29, label: 'Plant Id', hidden: true }]
  @UI.selectionField: [{ position: 29 }]
  PlantId;

  @EndUserText.label: 'Plant Name'
  @UI.identification: [{ position: 30, label: 'Plant Name' }]
  @UI.lineItem: [{ position: 30, label: 'Plant Name' }]
  @UI.selectionField: [{ position: 30 }]
  PlantName;

  @EndUserText.label: 'Country'
  @UI.identification: [{ position: 40, label: 'Country' }]
  @UI.lineItem: [{ position: 40, label: 'Country' }]
  @UI.selectionField: [{ position: 40 }]
  Country;

  @EndUserText.label: 'Current Stock'
  @UI.lineItem: [ {position: 50 , label: 'Current Stock'} ]
  @UI.dataPoint: { qualifier: 'CurrentStockData', title: 'Current Stock' }
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 10}]
  CurrentStock;

  @EndUserText.label: 'Stock Status'
  @UI.lineItem: [{ position: 60, criticality: 'StockStatus', label: 'Stock Status' }]
  @UI.textArrangement: #TEXT_ONLY
  @UI.selectionField: [{ position: 60 }]
  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status', criticality: 'StockStatus' }
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 20}]
  StockStatus;

  @EndUserText.label: 'Days of Stock'
  @UI.lineItem: [{ position: 70, label: 'Days of Stock' }]
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 30}]
  DaysOfStock;

  @EndUserText.label: 'Min. Stock Quantity'
  @UI.lineItem: [ {position: 71 , label: 'Minimum Stock', hidden: true} ]
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 40}]
  MinimumStock;

  @EndUserText.label: 'Max. Stock Quantity'
  @UI.lineItem: [ {position: 73 , label: 'Maximum Stock', hidden: true} ]
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 50}]
  MaximumStock;

  @EndUserText.label: 'Reorder Point Qty'
  @UI.lineItem: [ {position: 75 , label: 'Reorder Point'} ]
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 60}]
  ReorderPoint;

  @EndUserText.label: 'Safety Stock Quantity'
  @UI.lineItem: [{ position: 80 , label: 'Safety Stock', hidden: true }]
  @UI.fieldGroup: [{qualifier: 'StocksGroup', position: 70}]
  SafetyStock;

  @EndUserText.label: 'Last Goods Receipt'
  @UI.lineItem: [ {position: 85, label: 'Last Goods Receipt', hidden: true} ]
  @UI.selectionField: [{ position: 85} ]
  @UI.fieldGroup: [{qualifier: 'PurchGroup', position: 10}]
  LastGoodsReceipt;

  @EndUserText.label: 'Last Goods Issue'
  @UI.lineItem: [{ position: 90, label: 'Last Goods Issue' }]
  @UI.selectionField: [{ position: 90 }]
  @UI.fieldGroup: [{qualifier: 'PurchGroup', position: 20}]
  LastGoodsIssue;

  @EndUserText.label: 'Stock Value'
  @UI.lineItem: [{ position: 100, label: 'Stock Value' }]
  @UI.fieldGroup: [{qualifier: 'PurchGroup', position: 30}]
  StockValue;

}